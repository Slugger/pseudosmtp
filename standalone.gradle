/*
 Copyright 2015 Battams, Derek
 
	Licensed under the Apache License, Version 2.0 (the "License");
	you may not use this file except in compliance with the License.
	You may obtain a copy of the License at
 
		http://www.apache.org/licenses/LICENSE-2.0
 
	Unless required by applicable law or agreed to in writing, software
	distributed under the License is distributed on an "AS IS" BASIS,
	WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
	See the License for the specific language governing permissions and
	limitations under the License.
*/

plugins {
	id 'groovy'
	id 'application'
	id 'eclipse'
	id 'net.nemerosa.versioning' version '1.3.0'
}
apply from: 'common.gradle'

defaultTasks 'clean', 'test', 'distZip'

mainClassName = 'com.github.pseudosmtp.standalone.Main'

dependencies {
	runtime 'commons-cli:commons-cli:1.2'	
}

sourceSets {
	main {
		resources.srcDirs = ['src/main/resources', 'src/main/webapp']
	}
}

task cpWarToDocker(type: Copy) {
	def outDir = new File(project.buildDir, 'docker/war')
	dependsOn war
	from(new File(project.buildDir, 'libs').absolutePath) {
		include war.archiveName
	}
	into(outDir.absolutePath)
	outputs.files(new File(outDir, war.archiveName))
}

task setupDockerEnv(type: Copy) {
	from(new File(project.projectDir, 'docker'))
	into(new File(project.buildDir, 'docker'))
}

task mkDockerImg(type: Exec) {
	dependsOn setupDockerEnv, cpWarToDocker
	workingDir = new File(project.buildDir, 'docker')
	commandLine 'docker', 'build', '-t', 'slugger/pseudosmtp', '.'
	doFirst {
		if(!System.getProperty('os.name').toLowerCase().contains('linux'))
			throw new UnsupportedOperationException('Can only build docker images on Linux!')
	}
}